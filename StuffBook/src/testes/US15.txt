# User Story 15 - Histórico de atividades do usuário

zerarSistema

# entradas válidas

criarUsuario login="mark" nome="Mark Zuckerberg" endereco="Palo Alto, California"
criarUsuario login="steve" nome="Steven Paul Jobs" endereco="Palo Alto, California"
criarUsuario login="bill" nome="William Henry Gates III" endereco="Medina, Washington"

sessaoMark=abrirSessao login="mark"
sessaoSteve=abrirSessao login="steve"
sessaoBill=abrirSessao login="bill"

requisitarAmizade idSessao=${sessaoSteve} login="mark"
requisitarAmizade idSessao=${sessaoBill} login="mark"

expect "Não há atividades" historicoAtividades idSessao=${sessaoMark}
expect "Não há atividades" historicoAtividades idSessao=${sessaoSteve}

aprovarAmizade idSessao=${sessaoMark} login="steve"
aprovarAmizade idSessao=${sessaoMark} login="bill"

expect "Mark Zuckerberg e William Henry Gates III são amigos agora; Mark Zuckerberg e Steven Paul Jobs são amigos agora" historicoAtividades idSessao=${sessaoMark}
expect "Steven Paul Jobs e Mark Zuckerberg são amigos agora" historicoAtividades idSessao=${sessaoSteve}

idItem1=cadastrarItem idSessao=${sessaoMark} nome="The Social Network" descricao="Filme sobre a fundação da rede social Facebook" categoria="Filme"
expect "Mark Zuckerberg cadastrou The Social Network; Mark Zuckerberg e William Henry Gates III são amigos agora; Mark Zuckerberg e Steven Paul Jobs são amigos agora" historicoAtividades idSessao=${sessaoMark}

idRequisicaoEmprestimo1=requisitarEmprestimo idSessao=${sessaoSteve} idItem=${idItem1} duracao=7
idEmprestimo1=aprovarEmprestimo idSessao=${sessaoMark} idRequisicaoEmprestimo=${idRequisicaoEmprestimo1}
expect "Mark Zuckerberg emprestou The Social Network a Steven Paul Jobs; Mark Zuckerberg cadastrou The Social Network; Mark Zuckerberg e William Henry Gates III são amigos agora; Mark Zuckerberg e Steven Paul Jobs são amigos agora" historicoAtividades idSessao=${sessaoMark}

expect "William Henry Gates III e Mark Zuckerberg são amigos agora" historicoAtividades idSessao=${sessaoBill}
registrarInteresse idSessao=${sessaoBill} idItem=${idItem1}
expect "William Henry Gates III manifestou interesse pelo item The Social Network de Mark Zuckerberg; William Henry Gates III e Mark Zuckerberg são amigos agora" historicoAtividades idSessao=${sessaoBill}

devolverItem idSessao=${sessaoSteve} idEmprestimo=${idEmprestimo1}
confirmarTerminoEmprestimo idSessao=${sessaoMark} idEmprestimo=${idEmprestimo1}
expect "Mark Zuckerberg confirmou o término do empréstimo do item The Social Network; Mark Zuckerberg emprestou The Social Network a Steven Paul Jobs; Mark Zuckerberg cadastrou The Social Network; Mark Zuckerberg e William Henry Gates III são amigos agora; Mark Zuckerberg e Steven Paul Jobs são amigos agora" historicoAtividades idSessao=${sessaoMark}

# entradas inválidas

expectError "Sessão inválida" historicoAtividades idSessao=
expectError "Sessão inválida" historicoAtividades idSessao=""
expectError "Sessão inexistente" historicoAtividades idSessao="xpto"

encerrarSistema
quit