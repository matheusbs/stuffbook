# User Story 09 - Requisitar devolução

zerarSistema

# entradas válidas

criarUsuario login="mark" nome="Mark Zuckerberg" endereco="Palo Alto, California"
criarUsuario login="steve" nome="Steven Paul Jobs" endereco="Palo Alto, California"
criarUsuario login="bill" nome="William Henry Gates III" endereco="Medina, Washington"

sessaoMark=abrirSessao login="mark"
sessaoSteve=abrirSessao login="steve"
sessaoBill=abrirSessao login="bill"

requisitarAmizade idSessao=${sessaoSteve} login="mark"
requisitarAmizade idSessao=${sessaoBill} login="mark"

aprovarAmizade idSessao=${sessaoMark} login="steve"
aprovarAmizade idSessao=${sessaoMark} login="bill"

idItem1=cadastrarItem idSessao=${sessaoMark} nome="The Social Network" descricao="Filme sobre a fundação da rede social Facebook" categoria="Filme"
idItem2=cadastrarItem idSessao=${sessaoSteve} nome="Guia do mochileiro das galáxias" descricao="Serie de ficção científica criada por Douglas Adams" categoria="Livro"

idRequisicaoEmprestimo1=requisitarEmprestimo idSessao=${sessaoSteve} idItem=${idItem1} duracao=7
idEmprestimo1=aprovarEmprestimo idSessao=${sessaoMark} idRequisicaoEmprestimo=${idRequisicaoEmprestimo1}

requisitarDevolucao idSessao=${sessaoMark} idEmprestimo=${idEmprestimo1}
expect "mark-steve:The Social Network:Cancelado" getEmprestimos idSessao=${sessaoMark} tipo="todos"
expect "mark-steve:The Social Network:Cancelado" getEmprestimos idSessao=${sessaoSteve} tipo="todos"

idTopico1=enviarMensagem idSessao=${sessaoMark} destinatario="steve" assunto="Empréstimo do item The Social Network a Steven Paul Jobs" mensagem="Steve, I need the film back." idRequisicaoEmprestimo=${idRequisicaoEmprestimo1}

expect "Empréstimo do item The Social Network a Steven Paul Jobs" lerTopicos idSessao=${sessaoSteve} tipo="todos"
expect "Steven Paul Jobs solicitou o empréstimo do item The Social Network; Mark Zuckerberg soliticou a devolução do item The Social Network; Steve, I need the film back." lerMensagens idSessao=${sessaoSteve} idTopico=${idTopico1}

idRequisicaoEmprestimo2=requisitarEmprestimo idSessao=${sessaoMark} idItem=${idItem2} duracao=7
idEmprestimo2=aprovarEmprestimo idSessao=${sessaoSteve} idRequisicaoEmprestimo=${idRequisicaoEmprestimo2}

adicionarDias dias=7
requisitarDevolucao idSessao=${sessaoSteve} idEmprestimo=${idEmprestimo2}
expect "mark-steve:The Social Network:Cancelado; steve-mark:Guia do mochileiro das galáxias:Andamento" getEmprestimos idSessao=${sessaoMark} tipo="todos"
expect "steve-mark:Guia do mochileiro das galáxias:Andamento; mark-steve:The Social Network:Cancelado" getEmprestimos idSessao=${sessaoSteve} tipo="todos"

devolverItem idSessao=${sessaoMark} idEmprestimo=${idEmprestimo2}
confirmarTerminoEmprestimo idSessao=${sessaoSteve} idEmprestimo=${idEmprestimo2}
expect "mark-steve:The Social Network:Cancelado; steve-mark:Guia do mochileiro das galáxias:Completado" getEmprestimos idSessao=${sessaoMark} tipo="todos"
expect "steve-mark:Guia do mochileiro das galáxias:Completado; mark-steve:The Social Network:Cancelado" getEmprestimos idSessao=${sessaoSteve} tipo="todos"

# entradas inválidas

expectError "Sessão inválida" requisitarDevolucao idSessao= idEmprestimo="xpto"
expectError "Sessão inválida" requisitarDevolucao idSessao="" idEmprestimo="xpto"
expectError "Sessão inexistente" requisitarDevolucao idSessao="xpto" idEmprestimo="xpto"
expectError "Identificador do empréstimo é inválido" requisitarDevolucao idSessao=${sessaoMark} idEmprestimo=
expectError "Identificador do empréstimo é inválido" requisitarDevolucao idSessao=${sessaoMark} idEmprestimo=""
expectError "Empréstimo inexistente" requisitarDevolucao idSessao=${sessaoMark} idEmprestimo="xpto"

expectError "O usuário não tem permissão para requisitar a devolução deste item" requisitarDevolucao idSessao=${sessaoBill} idEmprestimo=${idEmprestimo1}
expectError "Devolução já requisitada" requisitarDevolucao idSessao=${sessaoMark} idEmprestimo=${idEmprestimo1}

devolverItem idSessao=${sessaoSteve} idEmprestimo=${idEmprestimo1}
expectError "Item já devolvido" requisitarDevolucao idSessao=${sessaoMark} idEmprestimo=${idEmprestimo1}

confirmarTerminoEmprestimo idSessao=${sessaoMark} idEmprestimo=${idEmprestimo1}
expect "steve-mark:Guia do mochileiro das galáxias:Completado; mark-steve:The Social Network:Cancelado" getEmprestimos idSessao=${sessaoSteve} tipo="todos"

expectError "Item já devolvido" requisitarDevolucao idSessao=${sessaoMark} idEmprestimo=${idEmprestimo1}

encerrarSistema
quit

